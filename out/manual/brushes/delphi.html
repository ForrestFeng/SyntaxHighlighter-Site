<!DOCTYPE html><html xmlns="http://www.w3.org/1999/xhtml"><head><title>NaN</title><script type="text/javascript" src="https://ajax.googleapis.com/ajax/libs/jquery/1.6.2/jquery.min.js"></script><meta charset="UTF-8">The partial "tracking" was not found, as such it will not be rendered.</head><body class="no_sponsor"><div id="title"><h1><a href="/SyntaxHighlighter">SyntaxHighlighter</a><span id="version"></span></h1><a id="forkme" href="https://github.com/alexgorbatchev/SyntaxHighlighter">Fork Me On GitHub</a><div id="whatsnew"><a href="/SyntaxHighlighter/whatsnew.html">whats new?</a></div><div id="blurb"><em>SyntaxHighlighter is an open source Java Script client side code syntax highlighter.</em></div><div id="meta"><a href="/SyntaxHighlighter/donate.html">donate</a><a href="/SyntaxHighlighter/download">download</a><a href="/SyntaxHighlighter/manual/installation.html">installation</a><a href="/SyntaxHighlighter/manual/api/autoloader.html">autoloader</a><a href="/SyntaxHighlighter/manual/configuration">configuration</a><a href="http://twitter.com/syntaxhighlight">syntaxhighlight</a></div><h2>Delphi Example</h2></div><div id="content"><pre class="brush: delphi">
{ $OmniXML: OmniXML/extras/OmniXMLShared.pas,v 1.1 2004/06/07 13:06:32 mr Exp $ }

{$WEAKPACKAGEUNIT ON}

(*:XML helper unit. Contains class to manage shared XML document (stored in the
   resizable shared memory).
   @author Primoz Gabrijelcic
   @desc &lt;pre>
   (c) 2002 Primoz Gabrijelcic
   Free for personal and commercial use. No rights reserved.

   Author            : Primoz Gabrijelcic
   Creation date     : 2002-05-13
   Last modification : 2002-09-24
   Version           : 1.01
&lt;/pre>*)(*
   History:
     1.01: 2002-09-24
       - Added property TGpSharedXML.CachedXML.
     1.0: 2002-05-13
       - Created.
*)

unit OmniXMLShared;

interface

uses
  Windows,
  GpSharedMemory,
  OmniXMLProperties;

type
  {:Shared XML document.
  }
  TGpSharedXML = class
  private
    sxOwnsXML     : boolean;
    sxSharedMemory: TGpSharedMemory;
    sxXMLDoc      : TGpXMLDoc;
  protected
    function  Acquire(forWriting: boolean; timeout: DWORD): TGpXMLDoc;
    function  GetName: string;
    function  GetXML: TGpXMLDoc;
  public
    constructor Create(sharedMemoryName: string; xmlDoc: TGpXMLDoc;
      maxSize: cardinal; ownsXML: boolean = true); virtual;
    destructor  Destroy; override;
    function  BeginUpdate(timeout: DWORD): TGpXMLDoc; virtual;
    procedure EndUpdate; virtual;
    function  Read(timeout: DWORD): TGpXMLDoc; virtual;
    property  CachedXML: TGpXMLDoc read sxXMLDoc;
    property  Name: string read GetName;
    property  XML: TGpXMLDoc read GetXML;
  end; { TGpSharedXML }

  {:Shared XML list.
  }
  TGpSharedXMLList = class(TGpSharedXML)
  public
    constructor Create(sharedMemoryName: string; xmlList: TGpXMLDocList;
      maxSize: cardinal; ownsXML: boolean = true); reintroduce; virtual;
    function Count: integer;
  end; { TGpSharedXMLList }

implementation

uses
{$IFDEF DebugXML}
  OmniXMLUtils,
  uDbg,
{$ENDIF DebugXML}
  SysUtils;

{ TGpSharedXML }

function TGpSharedXML.Acquire(forWriting: boolean;
  timeout: DWORD): TGpXMLDoc;
begin
  Result := nil;
  if sxSharedMemory.AcquireMemory(forWriting, timeout) &lt;> nil then begin
      if (not sxSharedMemory.Modified) or
         sxXMLDoc.LoadFromStream(sxSharedMemory.AsStream)
      then
        Result := sxXMLDoc;
    if not assigned(Result) then
      sxSharedMemory.ReleaseMemory;
    {$IFDEF DebugXML}
    if assigned(Result) then
      Debugger.LogFmtMsg('Acquire: %s',[XMLSaveToString(sxXMLDoc.XMLDoc)]);
    {$ENDIF DebugXML}
  end;
end; { TGpSharedXML.Acquire }

function TGpSharedXML.BeginUpdate(timeout: DWORD): TGpXMLDoc;
begin
  Result := Acquire(true, timeout);
end; { TGpSharedXML.BeginUpdate }

constructor TGpSharedXML.Create(sharedMemoryName: string; xmlDoc: TGpXMLDoc;
  maxSize: cardinal; ownsXML: boolean);
begin
  inherited Create;
  sxSharedMemory := TGpSharedMemory.Create(sharedMemoryName, 0, maxSize);
  sxXMLDoc := xmlDoc;
  sxOwnsXML := ownsXML;
end; { TGpSharedXML.Create }

destructor TGpSharedXML.Destroy;
begin
  if sxOwnsXML then
    FreeAndNil(sxXMLDoc)
  else
    sxXMLDoc := nil;
  FreeAndNil(sxSharedMemory);
  inherited;
end; { TGpSharedXML.Destroy }

procedure TGpSharedXML.EndUpdate;
begin
  {$IFDEF DebugXML}
  Debugger.LogFmtMsg('Release: %s',[XMLSaveToString(sxXMLDoc.XMLDoc)]);
  {$ENDIF DebugXML}
  if sxSharedMemory.IsWriting then
    sxXMLDoc.SaveToStream(sxSharedMemory.AsStream);
  if sxSharedMemory.Acquired then
    sxSharedMemory.ReleaseMemory;
end; { TGpSharedXML.EndUpdate }

function TGpSharedXML.GetName: string;
begin
  Result := sxSharedMemory.Name;
end; { TGpSharedXML.GetName }

function TGpSharedXML.GetXML: TGpXMLDoc;
begin
  if not sxSharedMemory.Acquired then
    Result := nil
  else
    Result := sxXMLDoc;
end; { TGpSharedXML.GetXML }

function TGpSharedXML.Read(timeout: DWORD): TGpXMLDoc;
begin
  Result := Acquire(false, timeout);
  EndUpdate;
end; { TGpSharedXML.Read }

{ TGpSharedXMLList }

function TGpSharedXMLList.Count: integer;
begin
  Result := (XML as TGpXMLDocList).Count;
end; { TGpSharedXMLList.Count }

constructor TGpSharedXMLList.Create(sharedMemoryName: string;
  xmlList: TGpXMLDocList; maxSize: cardinal; ownsXML: boolean);
begin
  inherited Create(sharedMemoryName, xmlList, maxSize, ownsXML);
end; { TGpSharedXMLList.Create }

{$IFDEF DebugXML}
initialization
  NxStartDebug;
finalization
  NxEndDebug;
{$ENDIF DebugXML}
end.
</pre>

<p>The partial &quot;brushes&quot; was not found, as such it will not be rendered.</p>
</div><div id="clear"></div><div id="copyright"><div>SyntaxHighlighter was originally created in 2004 and is still maintained by Alex Gorbatchev.</div><div>Copyright 2004-2014 Alex Gorbatchev.</div></div><div id="footer"><div id="ad"><script>google_ad_client = "ca-pub-2888483682038752";
/* SyntaxHighlighter2 */
google_ad_slot = "1794546701";
google_ad_width = 160;
google_ad_height = 600;</script><script type="text/javascript" src="http://pagead2.googlesyndication.com/pagead/show_ads.js"></script></div><a href="/SyntaxHighlighter/about.html">about</a><a href="/SyntaxHighlighter/manual/brushes/">syntaxes</a><a href="/SyntaxHighlighter/manual/themes/">themes</a><a href="/SyntaxHighlighter/integration.html">integration</a><a href="/SyntaxHighlighter/hosting.html">hosting</a><a href="/SyntaxHighlighter/manual/older_versions.html">legacy</a><a href="/SyntaxHighlighter/manual/api">apis</a><a href="https://github.com/alexgorbatchev/SyntaxHighlighter/issues">issues</a><a href="https://github.com/alexgorbatchev/SyntaxHighlighter">source</a><a href="https://github.com/alexgorbatchev/SyntaxHighlighter-Site">site</a><a href="/SyntaxHighlighter/contact.html">contact</a></div></body><link type="text/css" rel="stylesheet" href="/SyntaxHighlighter/css/main.css"><link type="text/css" rel="stylesheet" href="/sh_pub/styles/shCore.css"><link type="text/css" rel="stylesheet" href="/sh_pub/styles/shThemeDefault.css"><script type="text/javascript" src="/sh_pub/scripts/shCore.js"></script><script type="text/javascript" src="/sh_pub/scripts/shAutoloader.js"></script><script type="text/javascript" src="/sh_pub/scripts/shBrushXml.js"></script><script type="text/javascript" src="/sh_pub/scripts/shBrushJScript.js"></script><script type="text/javascript" src="/sh_pub/scripts/shBrushCss.js"></script><script type="text/javascript" src="/SyntaxHighlighter/scripts/main.js"></script>The partial "services" was not found, as such it will not be rendered.</html>